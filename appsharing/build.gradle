apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'com.tappytaps.android'
archivesBaseName = 'app-sharing'
version = '1.1.0'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def myMavenRepoWriteUrl = properties.getProperty('myMavenRepoWriteUrl')
def myMavenRepoWritePassword = properties.getProperty('myMavenRepoWritePassword')

publishing {
    repositories {
        maven {
            url myMavenRepoWriteUrl
            credentials {
                username 'myMavenRepo'
                password myMavenRepoWritePassword
            }
        }
    }

    publications {
        aar(MavenPublication) {
            groupId group
            artifactId archivesBaseName
            version project.version

            // Optional
            pom.withXml {
                asNode().appendNode('name', 'App Sharing')
                asNode().appendNode('description', 'TappyTaps App Sharing Library')

                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each { dependency ->
                    if (dependency.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }

            artifact("$buildDir/outputs/aar/${archivesBaseName}-release.aar")        }
    }
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName version

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.fragment:fragment:1.2.5"

    // mailer
    implementation 'de.cketti.mailto:email-intent-builder:2.0.0'

    // facebook share
    implementation 'com.facebook.android:facebook-share:7.1.0'

    // qr codes
    implementation 'com.google.zxing:core:3.4.0'
    implementation 'com.journeyapps:zxing-android-embedded:4.1.0@aar'

    testImplementation 'junit:junit:4.13.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}